include "root" {
  path           = find_in_parent_folders()
  expose         = true
  merge_strategy = "deep"
}

include "env" {
  path           = find_in_parent_folders("env.hcl")
  expose         = true
  merge_strategy = "deep"
}

locals {
  az_count = length(include.env.locals.values.azs)
  cidr_subnets     = [for cidr_block in cidrsubnets(include.env.locals.values.cidr_block, 2, 2, 2, 2) : cidrsubnets(cidr_block, 4, 4, 4, 4)]
  private_subnets  = chunklist(local.cidr_subnets[0], local.az_count)[0]
  public_subnets   = chunklist(local.cidr_subnets[1], local.az_count)[0]
  database_subnets = chunklist(local.cidr_subnets[2], local.az_count)[0]
  intra_subnets    = chunklist(local.cidr_subnets[3], local.az_count)[0]
}

terraform {
  source = "tfr:///terraform-aws-modules/vpc/aws//?version=5.5.1"
}

inputs = {
  name = include.env.locals.values.name
  cidr = include.env.locals.values.cidr_block

  azs              = include.env.locals.values.azs
  private_subnets  = local.private_subnets
  public_subnets   = local.public_subnets
  database_subnets = local.database_subnets
  intra_subnets    = local.intra_subnets

  single_nat_gateway      = include.env.locals.values.single_nat_gateway
  enable_nat_gateway      = true
  enable_vpn_gateway      = false
  enable_dns_hostnames    = true
  enable_dns_support      = true
  map_public_ip_on_launch = true

  create_database_subnet_group = false

  manage_default_security_group  = true
  default_security_group_ingress = []
  default_security_group_egress  = []

  tags = merge(include.env.locals.tags, {
    "kubernetes.io/cluster/${include.env.locals.values.name}" = "shared"
  })

  private_subnet_tags = {
    Name                              = "${include.env.locals.values.name}-private"
    destination                       = "private"
    "karpenter.sh/discovery"          = "private"
    "kubernetes.io/role/internal-elb" = "1"
  }

  private_route_table_tags = {
    Name        = "${include.env.locals.values.name}-private"
    destination = "private"
  }

  public_subnet_tags = {
    Name                     = "${include.env.locals.values.name}-public"
    destination              = "public"
    "karpenter.sh/discovery" = "public"
    "kubernetes.io/role/elb" = "1"
  }

  public_route_table_tags = {
    Name        = "${include.env.locals.values.name}-public"
    destination = "public"
  }

  database_subnet_tags = {
    Name        = "${include.env.locals.values.name}-database"
    destination = "database"
  }

  database_route_table_tags = {
    Name        = "${include.env.locals.values.name}-database"
    destination = "database"
  }

  intra_subnet_tags = {
    Name        = "${include.env.locals.values.name}-intra"
    destination = "intra"
  }

  intra_route_table_tags = {
    Name        = "${include.env.locals.values.name}-intra"
    destination = "intra"
  }
}